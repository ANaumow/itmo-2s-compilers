-- Expression evaluator

import List;
import State;

-- As association map which maps "\otimes" into "\oplus"
local ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- The evaluator itself: takes a state and an expression,
-- return integer
public fun evalExpr (st, expr) {
  case expr of
    Var   (x)        -> st (x)
  | Const (n)        -> n
  | Binop (op, l, r) -> evalOp (op, evalExpr (st, l), evalExpr (st, r))     
  esac
}

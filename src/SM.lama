-- Stack machine.

import Array;
import List;
import Fun;
import Collection;
import World;
import State;
import Expr;
import Buffer;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  case i of
    READ         -> sprintf ("READ")
  | WRITE        -> sprintf ("WRITE")
  | BINOP (s)    -> sprintf ("BINOP %s", s)
  | LD    (x)    -> sprintf ("LD %s", x)
  | ST    (x)    -> sprintf ("ST %s", x)
  | CONST (n)    -> sprintf ("CONST %d", n)
  | LABEL (s)    -> sprintf ("LABEL %s", s)
  | JMP   (l)    -> sprintf ("JMP %s", l)
  | CJMP  (c, l) -> sprintf ("CJMP %s, %s", c, l)
  esac
}

public fun showSM (prg) {
  map (fun (i) { showSMInsn (i) ++ "\n" }, prg).stringcat
}

-- Evaluation environment: keeps a mapping between labels and (sub)programs
fun initEvalEnv (insns) {
  var map =
    fix (fun (rec) {
          fun ([m, insns]) {
            case insns of
              {}               -> m
            | LABEL (lab) : tl -> rec ([addMap (m, lab, insns), tl])
            | _ : tl           -> rec ([m, tl])
            esac
          }
        }) $ [emptyMap (compare), insns];

  [fun (l) {
     case findMap (map, l) of Some (insns) -> insns esac
  }]
}

-- Accessor function
fun fromLabel (env, lab) {
  env [0] (lab)
}

fun doJump(c, n) {
  case c of
      "z"  -> n == 0
    | "nz" -> n != 0
  esac
}

-- Stack machine interpreter. Takes an environment, an SM-configuration and a program,
-- returns a final configuration
fun eval (env, c@[stack, state, w], insns) {
  case insns of
      { }       -> c
    | i : insns ->
      case i of
          READ         ->
            case readWorld(w) of
              [n, w] -> eval(env, [n:stack, state, w], insns)
            esac
        | WRITE        ->
          case stack of
            n : st -> eval(env, [st, state, writeWorld(n, w)], insns)
          esac
        | BINOP (op)   ->
          case stack of
            x : y : st -> eval(env, [evalOp(op, y, x):st, state, w], insns)
          esac
        | CONST (x)    -> eval(env, [x:stack, state, w], insns)
        | LD (x)       -> eval(env, [state(x):stack, state, w], insns)
        | ST (x)       ->
          case stack of
            top : stack -> eval(env, [stack, state <- [x, top], w], insns)
          esac
        | LABEL (_)    -> eval(env, [stack, state, w], insns)
        | JMP   (l)    -> eval(env, [stack, state, w], env.fromLabel(l))
        | CJMP  (c, l) ->
          case stack of
            n : st -> eval(env, [st, state, w], if doJump(c, n) then env.fromLabel(l) else insns fi)
          esac
      esac
  esac
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval (initEvalEnv (insns), [{}, emptyState, createWorld (input)], insns)[2].getOutput
}

-- Compilation environment: generates labels
fun makeCompEnv (n) {
  [fun () {
     [sprintf ("L%d", n), makeCompEnv (n+1)]
  }]
}

fun initCompEnv () {
  makeCompEnv (0)
}

-- Accessor function: generates one label
fun genLabel (env) {
  env [0] ()
}

-- Utility function: generates n labels
fun genLabels (env, n) {
  fun inner (env, n) {
    if n == 0
    then {env}
    else case env.genLabel of
           [lab, env] -> lab : inner (env, n-1)
         esac
    fi
  }

  listArray (inner (env, n))
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine instructions
fun compileExpr (expr) {
  case expr of
      Var   (x)               -> singletonBuffer (LD (x))
    | Const (n)               -> singletonBuffer (CONST (n))
    | Binop (op, left, right) -> compileExpr (left) <+> compileExpr (right) <+ BINOP (op)
  esac
}

-- Compiles a statement into a stack machine code.
-- Takes a statement, returns a list of stack machine
-- instructions.
public fun compileSM (stmt) {
  -- Generates a buffer with an optional label
  --    lab     --- the name of the label
  --    labUsed --- a flag if the lebel is needed
  fun label (lab, labUsed) {
    if labUsed
    then singletonBuffer (LABEL (lab))
    else emptyBuffer ()
    fi
  }

  -- Generates SM code for a given statement
  --    lab  --- a label th go to after the statement is executed
  --    env  --- compilation environment
  --    stmt --- a statement to generate SM code for
  -- Returns a triple:
  --    boolean flag indicating if "lab" was used
  --    updated environment
  --    code buffer
  fun compile (lab, env, stmt) {
    case stmt of
      Skip           -> [false, env, emptyBuffer ()]
    | Read  (x)      -> [false, env, singletonBuffer (READ) <+ ST (x)]
    | Write (e)      -> [false, env, compileExpr (e) <+ WRITE]
    | Assn  (x, e)   -> [false, env, compileExpr (e) <+ ST (x)]
    | Seq   (s1, s2) ->
        case env.genLabel of
          [s2Lab, env] ->
             case compile (s2Lab, env, s1) of
               [s2LabUsed, env, s1Code] ->
                  case compile (lab, env, s2) of
                    [labUsed, env, s2Code] -> [labUsed, env, s1Code <+> label (s2Lab, s2LabUsed) <+> s2Code]
                  esac
             esac
        esac
      | If (expr, thenBody, elseBody) ->
        case env.genLabel of
          [l1, env] ->
            case compile (lab, env, thenBody) of
              [_, env, thenCode] ->
                case compile (lab, env, elseBody) of
                  [_, env, elseCode] ->
                    [true, env, compileExpr (expr) <+ CJMP ("z", l1) <+> thenCode <+ JMP (lab)
                      <+ LABEL (l1) <+> elseCode <+ JMP (lab)]
                esac
            esac
        esac
      | While (expr, body) ->
        case env.genLabel of
          [l1, env] ->
            case env.genLabel of
              [l2, env] ->
                case compile (l2, env, body) of
                  [_, env, bodyCode] ->
                    [false, env, singletonBuffer (JMP (l2)) <+ LABEL (l1) <+> bodyCode
                      <+ LABEL (l2) <+> compileExpr (expr) <+ CJMP ("nz", l1)]
                esac
            esac
        esac
      | DoWhile (body, e)  ->
        case env.genLabel of
          [l1, env] ->
            case env.genLabel of
              [l2, env] ->
                case compile (l2, env, body) of
                  [isUsing, env, code] ->
                    [false, env, singletonBuffer (LABEL (l1)) <+> code
                      <+> label (l2, isUsing) <+> compileExpr (e) <+ CJMP ("nz", l1)]
                esac
            esac
        esac
      | _  -> failure ("Unknown statement: %s\n", stmt.string)
    esac
  }

  case initCompEnv ().genLabel of
    [endLab, env] ->
       case compile (endLab, env, stmt) of
         [endLabUsed, _, code] -> getBuffer $  code <+> label (endLab, endLabUsed)
       esac
  esac
}
